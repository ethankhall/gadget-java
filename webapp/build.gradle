plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm'
    id 'application'
    id 'com.google.cloud.tools.jib' version '0.10.0'
}

dependencies {
    compile project(":database-manager")
    compile project(":database-migration")

    compile deps.libraries.logging
    compile deps.libraries.common
    compile deps.libraries.kotlin
    compile deps.libraries.jackson
    compile deps.libraries.spring

    compile deps.libraries.freemarker

    annotationProcessor deps.libraries.springAnnotation

    testCompile deps.libraries.testingLibraries
}

mainClassName = 'io.ehdev.gadget.webapp.GadgetApplication'

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

jib {
    to {
        image = 'gcr.io/account-manager-216313/gadget-app'
        tags = [version.toString(), 'latest']
        auth {
            username = '_json_key'
        }
    }
    container {
        ports = [ '8080' ]
    }
}

task copyDistForRelease(type: Copy, dependsOn: 'distZip') {
  from distZip.archivePath
  into "$buildDir/gadget-release"
  rename distZip.archiveName, 'gadget.zip'
}

startScripts {
    doLast {
        windowsScript.text = windowsScript.text.replaceAll('set CLASSPATH=.*', 'set CLASSPATH=.;%APP_HOME%/lib/*')
    }
}