plugins {
    id 'java'
    id 'io.ehdev.gradle.development'
    id 'org.jetbrains.kotlin.jvm'
    id "org.jetbrains.kotlin.kapt"
}

dependencies {
    compile project(":database-jooq")
    compile project(":model")
    compile project(":shared-config")
    compile deps.libraries.kotlin
    compile deps.libraries.logging
    compile deps.libraries.database
    compile deps.libraries.dagger
    compile deps.libraries.commonsLang
    compile 'javax.annotation:javax.annotation-api:1.2'

    testCompile deps.libraries.testingLibraries

    kapt deps.libraries.daggerCompiler
}

integTest.dependsOn ':database-migration:flywayMigrate'

task prepareConfig(type: Copy) {
    from "$rootDir/test-config"
    into sourceSets.integTest.output.resourcesDir
    outputs.upToDateWhen { false }
    filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: [
            'URL' : database.url,
            'USER_NAME'  : database.user,
            'PASSWORD': database.password,
            'DB'   : database.name
    ])
}

tasks.integTestClasses.dependsOn(prepareConfig)


kapt {
    generateStubs = true
}