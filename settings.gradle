pluginManagement {
    repositories {
        gradlePluginPortal()
        mavenLocal()
        maven { url 'https://dl.bintray.com/ethankhall/gradle-plugins/' }
    }
    resolutionStrategy {
        eachPlugin { details ->
            def id = details.requested.id.id
            def latestVersion = "0.0.30"
            if (id in ['io.ehdev.gradle.dependency-resolve',
                       'io.ehdev.gradle.klint',
                       'io.ehdev.gradle.build-dir',
                       'io.ehdev.gradle.dependency-script',
                       'io.ehdev.gradle.development'] && details.requested.version == 'LATEST') {
                println "Using the latest version of $id, which is $latestVersion"
                details.useVersion(latestVersion)
            }
        }
    }
}

include 'database-jooq'
include 'database-manager'
include 'database-migration'
include 'model'
include 'webapp'
rootProject.name = 'gadget-java'